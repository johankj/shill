#lang shill/cap

provide {
  wget : {
    wallet : native-wallet/c,
    tcp : socket-factory/c(+address-family 2,
                           +transfer-family 1,
                           +permissions { +connect, +recv, +send, +poll, +bind, +accept, +listen },
                           +no-proxy),
    udp : socket-factory/c(+address-family 2,
                           +transfer-family 2,
                           +permissions { +connect, +recv, +send, +poll },
                           +no-proxy),
    readonly : listof(read-only/c)
  }
  +optional {
    output : file/c(+path, +write, +append),
    stdin  : maybe/c(readable/c),
    stdout : maybe/c(writeable/c),
    stderr : maybe/c(writeable/c),
    timeout : timeout/c
  }
  -> integer?
};

require shill/contracts shill/native;

val wget = fun (wallet, tcp, udp, readonly, output = false, stdin = false, stdout = false, stderr = false, timeout = false) {
  val executable = pkg-native("wget", wallet);

  val url = "ifconfig.co/json";
  val other_ = list-append([tcp, udp], readonly);
  vals args, extra =
       if output then {
          values(["-O", output, url], list-append([output], other_));
       } else {
          values(["-qO", "-", url], other_);
       };

  executable(args, timeout = timeout, stdin = stdin, stdout = stdout, stderr = stderr, extra = extra);
}
